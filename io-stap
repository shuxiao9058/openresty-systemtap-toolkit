#!/usr/bin/env perl

use 5.006001;
use strict;
use warnings;

use Getopt::Long qw( GetOptions );

GetOptions("h",         \(my $help),
           "p=i",       \(my $pid))
    or die usage();

if ($help) {
    print usage();
    exit;
}

if (!defined $pid) {
    die "No process pid specified by the -p option.\n";
}

my $stap_args = " -DMAXACTION=100000 -DMAXBACKTRACE=200 -DMAXSTRINGLEN=2048";


my $stap_src;
$stap_src = <<_EOC_;
probe begin {
	printf("begin to probe\\n")
}

probe ioblock.request {
	if (pid() == target()) {
		printf("%d %s: %s %d + %d %s%s%s\\n",
				tid(), execname(), devname, sector, size,
				rw ? "W":"R", opf & 0x10 ? "S":"", opf & 0x1000 ? "F":"")
		print_ubacktrace()
		printf("\\n")
	}
}

probe timer.s(120) {
	printf("end to probe\\n")
	exit()
}
_EOC_


#print $stap_src;

my $d_so_args = '';

my %so_files;
{
	my $maps_file = "/proc/$pid/maps";
	open my $in, $maps_file
		or die "Cannot open $maps_file for reading: $!\n";

	while (<$in>) {
		if (/\s+(\/\S+\.so(?:\.\S+)?)$/) {
			if (!exists $so_files{$1}) {
				$so_files{$1} = 1;
				#warn $1, "\n";
			}
		}
	}

	close $in;
}

if (%so_files) {
	$d_so_args = join " ", map { "-d $_" } sort keys %so_files;
}

my $exec_file = "/proc/$pid/exe";
if (!-f $exec_file) {
	die "User process $pid is not running or ",
		"you do not have enough permissions.\n"
}
my $exec_path = readlink $exec_file;

open my $in, "|stap --skip-badvars --all-modules -x $pid -d '$exec_path' --ldd $d_so_args $stap_args -"
	or die "Cannot run stap: $!\n";

print $in $stap_src;

close $in;
	
sub usage {
    return <<'_EOC_';
Usage:
    osd-stap [optoins]

Options:
    -h                  Print this usage.
    -p <pid>            Specify the user process pid.

Examples:
    osd-stap -p 12345
_EOC_
}
